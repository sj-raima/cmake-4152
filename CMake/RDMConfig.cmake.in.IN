@PACKAGE_INIT@
if (RDM_STATIC)
    set (RDM_STATIC_OR_SHARED_TARGETS "RDMStaticTargets.cmake")
else ()
    set (RDM_STATIC_OR_SHARED_TARGETS "RDMSharedTargets.cmake")
endif ()


message (DEBUG "Processing: ${RDM_STATIC_OR_SHARED_TARGETS}")
if (NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/${RDM_STATIC_OR_SHARED_TARGETS}")
    if (RDM_STATIC)
        message (STATUS "RDM libraries: Using shared libraries instead of static libraries")
        set (RDM_STATIC_OR_SHARED_TARGETS "RDMSharedTargets.cmake")
    else ()
        message (STATUS "RDM libraries: Using static libraries instead of shared libraries")
        set (RDM_STATIC_OR_SHARED_TARGETS "RDMStaticTargets.cmake")
  endif ()
endif ()
message (DEBUG "Processing: ${RDM_STATIC_OR_SHARED_TARGETS}")
include ("${CMAKE_CURRENT_LIST_DIR}/${RDM_STATIC_OR_SHARED_TARGETS}")

if (CMAKE_CROSSCOMPILING)
  if (NOT DEFINED RDM_TARGET)
    message (FATAL_ERROR "RDM_TARGET is not defined in the toolchain file")
  endif ()
  message (DEBUG "Processing: ${CMAKE_CURRENT_LIST_DIR}/../../../../lib/cmake/RDM/RDMDevelopmentToolsTargets.cmake")
  include ("${CMAKE_CURRENT_LIST_DIR}/../../../../lib/cmake/RDM/RDMDevelopmentToolsTargets.cmake")
else ()
  message (DEBUG "Processing: ${CMAKE_CURRENT_LIST_DIR}/RDMDevelopmentToolsTargets.cmake")
  include ("${CMAKE_CURRENT_LIST_DIR}/RDMDevelopmentToolsTargets.cmake")
endif ()

if (EXISTS "${CMAKE_CURRENT_LIST_DIR}/RDMTestToolsTargets.cmake")
    message (DEBUG "Processing: ${CMAKE_CURRENT_LIST_DIR}/RDMTestToolsTargets.cmake")
    include ("${CMAKE_CURRENT_LIST_DIR}/RDMTestToolsTargets.cmake")
endif ()

set (RDM_JAVA_TARGET_DIR "${CMAKE_CURRENT_LIST_DIR}/../../../share/cmake/RDM")
if (EXISTS "${RDM_JAVA_TARGET_DIR}/RDMJarTargets.cmake")
    message (DEBUG "Processing ${RDM_JAVA_TARGET_DIR}/RDMJarTargets.cmake")
    include ("${RDM_JAVA_TARGET_DIR}/RDMJarTargets.cmake")
endif ()
check_required_components ("RDM")

if (NOT DEFINED RDM_FOUND_QUIETLY)
    message (STATUS "Found RDM in: ${CMAKE_CURRENT_LIST_DIR}")
endif ()
